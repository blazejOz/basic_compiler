Program      ::= { Statement }

Statement    ::= VarDeclStmt
               | PrintStmt
               | ExprStmt
               | IfStmt
               | WhileStmt

VarDeclStmt  ::= "int" IDENT "=" Expr ";"
PrintStmt    ::= "print" "(" Expr ")" ";"
ExprStmt     ::= Expr ";"
IfStmt       ::= "if" "(" Expr ")" BlockStmt [ "else" BlockStmt ]
WhileStmt    ::= "while" "(" Expr ")" BlockStmt
BlockStmt        ::= "{" { Statement } "}"

Expr         ::= CompareExpr
CompareExpr  ::= AddExpr [ CompareOp AddExpr ]
CompareOp    ::= "==" | "!=" | "<" | "<=" | ">" | ">="

AddExpr      ::= Term { ("+" | "-") Term }
Term         ::= Factor { ("*" | "/") Factor }
Factor       ::= NUMBER | IDENT | "(" Expr ")"